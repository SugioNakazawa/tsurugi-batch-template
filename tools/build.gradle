plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'jacoco'
    id 'maven-publish'
}

group 'jp.gr.java_conf.nkzw.tbt'
version '${ICEAXE_VERSION}'

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'tools'
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/SugioNakazawa/tsurugi-batch-template")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

java {
    mainClassName = 'jp.gr.java_conf.nkzw.tbt.tools.TgData'
    toolchain {
        languageVersion = JavaLanguageVersion.of(JAVA_VERSION)
    }
}

shadowJar {
    mergeServiceFiles() // SPIの同名ファイルを結合する
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

dependencies {
    implementation group: 'org.apache.poi', name: 'poi', version: '4.1.2'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'
    implementation 'com.beust:jcommander:1.82'
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.11.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.11.2'
    // implementation 'com.google.guava:guava:33.4.8-jre'

    implementation group: 'org.graphper', name: 'graph-support', version: '1.3.1' 

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
}

jacoco {
    applyTo tasks.run // runタスクにJacocoを適用
}

test.finalizedBy jacocoTestReport

def coverageExcludeFiles = [
		// 内部クラスやLambda匿名クラスを排除
		'**/*$*.class',
		'**/*$lambda$*.class',
]

jacocoTestReport {
    reports {
        html.required = true // HTML レポートを有効化
        xml.required = false // XML レポートは無効化（必要に応じて変更可能）
    }
    // Jacoco のクラスファイル設定を後から評価して適用
    afterEvaluate {
        // クラスディレクトリのファイルリストを収集し、除外設定を適用
        classDirectories = files(classDirectories.files.collect {
            fileTree(
                dir: it, // 各クラスファイルのディレクトリ
                exclude: coverageExcludeFiles // 除外するクラスファイルパターン
            )
        })
    }
}